import java.time.Instant
import java.time.ZoneId
import java.time.format.DateTimeFormatter

buildscript {
    repositories {
        maven {
            url 'https://repo.eclipse.org/content/groups/releases/'
        }
    }

    dependencies {
        classpath group: 'org.eclipse.jgit', name: 'org.eclipse.jgit', version: '4.10.0.201712302008-r'
    }
}

plugins {
    id 'org.ajoberstar.grgit' version '2.1.0'
}

description = """The Theta Discord Bot"""

subprojects {
    ext {
        botDependencies = [
                JDA          : 'net.dv8tion:JDA:3.4.0_318',
                Guava        : 'com.google.guava:guava:23.6-jre',
                Math         : 'com.github.meowingtwurtle:mathParser:1.0.1',
                JGit         : 'org.eclipse.jgit:org.eclipse.jgit:4.9.2.201712150930-r',
                NoOpLogger   : 'org.slf4j:slf4j-nop:1.7.25',
                ApacheCommons: 'org.apache.commons:commons-lang3:3.6',
                ThetaAPI     : project(':theta-api'),
                ThetaAudio   : project(':theta-audio'),
                ThetaBase    : project(':theta-base'),
                ThetaCommands: project(':theta-commands'),
                ThetaRunner  : project(':theta-runner')
        ]
    }

    apply plugin: 'java'

    group = 'com.github.meowingtwurtle'

    def jgit = grgit.repository.jgit
    def logCommand = jgit.log().addPath(name)
    logCommand.maxCount = 1
    def commit = logCommand.call().first()
    def time = commit.commitTime

    version = Instant.ofEpochSecond(time).atZone(ZoneId.of("UTC")).format(DateTimeFormatter.ofPattern("uuuuMMddHHmm"))

    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

    repositories {
        mavenCentral()
        jcenter()
    }
}
